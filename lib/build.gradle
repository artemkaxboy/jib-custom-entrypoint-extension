// define all versioned plugins here and apply in subprojects as necessary without version
plugins {
    id 'com.github.sherter.google-java-format' version '0.9' apply false
}

subprojects {
    group 'com.artemkaxboy'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.sherter.google-java-format'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    /* PROJECT DEPENDENCY VERSIONS */
    // define all common versioned dependencies here
    project.ext.dependencyVersions = [

            JIB_CORE: '0.15.0',
            JIB_GRADLE: '2.7.0',

            // for Build Plan and Jib Plugins Extension API
            JIB_GRADLE_EXTENSION: '0.4.0',
            JIB_MAVEN_EXTENSION: '0.4.0',

            MAVEN_API: '3.6.3',

            //test
            JUNIT: '4.13.1',
            MOCKITO_CORE: '3.4.6',
            SLF4J_API: '1.7.30',
    ]
    /* PROJECT DEPENDENCY VERSIONS */

    /* GOOGLE JAVA FORMAT */
    googleJavaFormat {
        toolVersion = '1.6'
    }
    check.dependsOn verifyGoogleJavaFormat
    /* GOOGLE JAVA FORMAT */

    /* JAVADOC ENFORCEMENT */
    // Fail build on javadoc warnings
    tasks.withType(Javadoc) {
        options.addBooleanOption('Xwerror', true)
    }
    assemble.dependsOn javadoc
    /* JAVADOC ENFORCEMENT */

    /* JAR */
    jar {
        manifest {
            attributes 'Implementation-Title': project.name,
                    'Implementation-Version': version,
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'),
                    'Built-Gradle': gradle.gradleVersion
        }
    }
    /* JAR */

    /* PUBLISHING */
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                def staging = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshot = "https://oss.sonatype.org/content/repositories/snapshots"

                url = version.endsWith('SNAPSHOT') ? snapshot : staging

                credentials {
                    username = ""
                    password = ""
                }
            }
        }
    }
    /* PUBLISHING */

    /* SIGNING */
    signing {
        sign publishing.publications.mavenJava
    }
    /* SIGNING */
}
